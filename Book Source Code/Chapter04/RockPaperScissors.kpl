//Program: RockPaperScissors.kpl
//Created: 12/26/2007
//Version: 1.0
//Author: Jerry Lee Ford, Jr.
//Description: This Phrogram game pits the player against the computer
//in a computerized implementation of the Rock, Paper, Scissors 
//game.

Program RockPaperscissors

	//Global constant used to store the name of the game
	Constant GameTitle As String = "The Rock, Paper, Scissors Game"
	
	Method Main()

		Define Reply As Boolean = False     //Used to store player input
		Define Move As String              //Stores player Movees
		Define Msg As String                //Stores a display string
		Define ComputerMove As String       //Stores computer moves
		Define RandomNumber As Integer      //Stores the game's random #
		Define Result As String             //Stores the game results
		
        HideToolbar()   //Prevent the window's toolbar from being displayed
 		SetScreenSize (600, 400)    //Set the screen size to 600x400 pixels
        SetWindowTitle (GameTitle)  //Display the name of the game in the
		                            //Window's titlebar
        
		SetConsoleBackgroundColor(WhiteSmoke)        //Set background color
		
        // Set console font style to Bold, non-italic and non-underline
        SetConsoleFontStyle(True, False, False)
			
		//Outline the text of the game's welcome message
		Msg = "Welcome To " + GameTitle + "!  Are You Ready To Play?"
		
		//Display the game's welcome message in a popup dialog window and
		//prompt the player to click on Yes to play the game or No to quit
		Reply = Confirm(Msg, GameTitle)
		
		//Analyze the player's response
		If Reply = True Then            //If Reply equals True then the
                                        //player clicked on the Yes button
				
			While True                  //Create a loop that runs forever
	
				ClearConsole()          //Clear the console
				
				//Prompt the player to enter a Move
				Move = ConsoleReadLine("Enter your move:", False)		
				
				If Move = "ROCK" Or Move = "PAPER" Or Move = "SCISSORS" Then
				
					//Generate a number from 1 to 3
					RandomNumber = Random(1, 3)
					
				    //Assign the random number to a valid game move
					Select RandomNumber
						Case 1
							ComputerMove = "ROCK"
						End Case
						Case 2
							ComputerMove = "PAPER"
						End Case
						Case 3
							ComputerMove = "SCISSORS"
						End Case
					End Select		
					
				    //Determine the results of the game			
				    If Move = "ROCK" Then
						Select ComputerMove
							Case "ROCK"
								Result = "Tie!" 								
							End Case
							Case "PAPER"
								Result = "Computer wins!" 
							End Case
							Case "SCISSORS"
								Result = "Player wins!" 
							End Case 
						End Select
					End If

					If Move = "PAPER" Then
						Select ComputerMove
							Case "ROCK"
								Result = "Player wins!" 
							End Case
							Case "PAPER"
								Result = "Tie!" 
							End Case
							Case "SCISSORS"
								Result = "Computer wins!" 
							End Case
						End Select
					End If

					If Move = "SCISSORS" Then
						Select ComputerMove
							Case "ROCK"
								Result = "Computer wins!" 
							End Case
							Case "PAPER"
								Result = "Player wins!" 
							End Case
							Case "SCISSORS"
								Result = "Tie!" 
							End Case
						End Select
					End If
					
					ClearConsole()                       //Clear the console
						
					SetConsoleFontColor(DarkBlue)        //Set font color
					
					// Set font to Courier New, point 14
					SetConsoleFont("Courier New", 14)          
					
					//Display the game results
					ConsoleWriteLine("")
					ConsoleWriteLine("")
					ConsoleWriteLine("")			
					ConsoleWriteLine(" Player Move:   " + ToUpperCase(Move))
					ConsoleWriteLine("")	
					ConsoleWriteLine(" Computer Move: " + Computermove)
					ConsoleWriteLine("")	
					
					SetConsoleFontColor(DarkRed)            //Set font color
					
					ConsoleWriteLine(" Result:        " + Result)
					ConsoleWriteLine("")
					ConsoleWriteLine("")
					ConsoleWriteLine("")
					ConsoleWriteLine("")
					
					// Set font to Courier New, point 12
					SetConsoleFont("Courier New", 12) 
					
					SetConsoleFontColor(Black)              //Set font color
					ConsoleWriteLine(" Press any key to play a new game.")
					
					ConsoleReadKey()	//Wait for the player to press a key				
								
				Else	
					
					//The player has entered and invalid move
					Alert("Your move was invalid. Please try again.", GameTitle)	
					
				End If	
				
			End While
					
		Else    //Execute the following statements when the player clicks 
	            //on the No button
			
			//Display a message in a popup window inviting the player to 
			//play again another time
			Alert("OK. Perhaps another time.", GameTitle)		
			
		End If	

	End Method

End Program