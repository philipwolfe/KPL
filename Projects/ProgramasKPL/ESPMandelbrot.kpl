Program Mandelbrot

	Structure RGBColor
		R	As Int
		G	As Int
		B	As Int
	End Structure

	Define MapaDeColor As RGBColor[256]

	Method Main()
	
		SetDeviceCoordinates()
		SetScreenSize( 400, 400 )
		
		Pen( Off )
		MoveTo( 50, 25 )
		PrintLine( "Dibujando un fractal de Mandelbrot..." )
		PrintLine( " " )
		PrintLine( "Este es un proceso extremadamente complicado y lento." )
		PrintLine( "Te esperamos si es que decides salir a tomar aire ;)" )
		
		Define x As Decimal
		Define y As Decimal
		Define x1 As Decimal
		Define y1 As Decimal
		Define xx As Decimal
		Define xmin As Decimal
		Define xmax As Decimal
		Define ymin As Decimal
		Define ymax As Decimal
		
		Define ciclo As Int
		Define s As Int
		Define z As Int
		
		Define integralX As Decimal
		Define integralY As Decimal 
		
		xmin = -2.1
		ymin = -1.3
		xmax = 1.0
		ymax = 1.3
		
		Define ancho As Int = ScreenWidth()
		Define altura As Int = ScreenHeight()
		
		Define Porcentaje As Decimal
		Define val As Int
		
		integralX = (xmax - xmin) / ancho
		integralY = (ymax - ymin) / altura
		
		Define i As Int
		For i = 1 To ArrayLength(MapaDeColor)
			MapaDeColor[i].R = i * i Mod 255
			MapaDeColor[i].G = 0
			MapaDeColor[i].B = 255 - Max( i, 128 )
		Next 
		
		x = xmin
		For S = 1 To ancho - 1
		
			y = ymin
			
			BeginFrame()
			
			For Z = 1 To altura - 1
			
				x1 = 0
				y1 = 0
				ciclo = 0
				
				While ciclo < 100 And Sqrt( (x1 * x1) + (y1 * y1) ) < 2.0
				
					ciclo = ciclo + 1
					xx = (x1 * x1) - (y1 * y1) + x
					y1 = 2 * x1 * y1 + y
					x1 = xx
					
				End While
					
				ColorRGB( MapaDeColor[ciclo].R, MapaDeColor[ciclo].G, MapaDeColor[ciclo].B )
				PutPixel( s, z )
				
				y = y + integralY
			
			Next
			
			RefreshScreen()
			
			x = x + integralX
			Status( "X=" + X )
		
		Next
	
		Status( "La operación tomó " + RoundToPlace( TickCount() / 1000, 2 ) + " segundos." )
		PlaySound( "Tada" )
		
	End Method 
	
End Program 
